   10 REM >PROFL
   20 *K.0MO.7|MVDU14|ML.1590,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~O%,~P%|M
   60 *K.4*SAVE :1.C.CODES 5800 +0300 2B00 2B00|M
   70 *K.10O.|M*FX12,2|M
   80 *TV255,1
   90 alred=&34
  100 holds=&3C
  110 taget=&5D
  120 tempa=&70
  130 tempx=&71
  140 tempy=&72
  150 tempz=&73
  160 indir=&74
  170 twice=&90
  180 condi=&92:techs=&92
  190 berth=&93
  200 depth=&94
  210 volum=&95
  220 sixtn=&A10
  230 money=&A00
  240 magns=&A03
  250 fract=&A04
  260 minut=&A05
  270 galax=&A06
  280 ships=&A0D
  290 smine=&A0F
  300 sixtn=&A10
  310 chaff=&A39
  320 headu=&A3F
  330 eurek=&A44
  340 navig=&A46
  350 phexa=&A47
  360 rentr=&A48
  370 missn=&A49
  380 finan=&A4A
  390 mmodl=&A4F
  400 buffr=&AD0
  410 WORDS=&4200
  420 DECIM=&4D83
  430 MDULE=&4D8F
  440 NOISE=&9806
  450 TECHS=&9C15
  460 CHART=&AB00
  470 CLEAN=&AF06
  480 WHITE=&AF09
  490 PRYNT=&AF0C
  500 NAMES=&B580
  510 BUFFR=&B583
  520 RELAT=&B780
  530 FORA%=4TO6STEP2
  540 O%=&5800
  550 P%=&2B00
  560 [OPTA%
  570 .PROFL JMPA
  580 .CONDI JMPB
  590 .PROGS JMPC
  600 .PAUSE JMPD
  610  
  620 .profl
  630 EQUW&A1F:EQUB0
  640 EQUS"PORT PROFILE"
  650 EQUW&11F:EQUB3:EQUS"NAME"
  660 EQUW&11F:EQUB5:EQUS"CONDITION"
  670 EQUW&11F:EQUB7
  680 EQUS"OPERATING INDEX  :"
  690 EQUW&11F:EQUB9
  700 EQUS"BERTHING CHARGE  :"
  710 EQUW&11F:EQUB11
  720 EQUS"DEPTH"
  730 EQUW&11F:EQUB13
  740 EQUS"ADMINISTRATION :"
  750 EQUW&11F:EQUB15
  760 EQUS"TRADE VOLUME"
  770 EQUW&101F:EQUB3:EQUS":"
  780 EQUW&101F:EQUB5:EQUS":"
  790 EQUW&121F:EQUB11:EQUS":"
  800 EQUW&101F:EQUB15:EQUS":"
  810 EQUW&181F:EQUB9:EQUS"N$"
  820 EQUW&181F:EQUB11:EQUS"NKm"
  830 EQUW&161F:EQUB15:EQUS"GN$"
  840 .endof
  850  
  860 .A
  870 JSRCLEAN
  880 JSRWHITE
  890 LDX#profl AND255:LDY#profl DIV256
  900 LDA#endof-profl:JSRPRYNT
  910 JSRRELAT
  920 LDAtaget:JSRNAMES
  930 LDAtaget:JSRCONDI
  940 LDA#&12:STAprynt+&1
  950 LDA#&3:STAprynt+&2
  960 LDX#&0
  970 .track
  980 LDAsixtn+&10,X:BEQatzer
  990 STAblock,X:INX:BNEtrack
 1000 .atzer
 1010 TXA:CLC:ADC#block-prynt
 1020 LDX#prynt AND255:LDY#prynt DIV256
 1030 JSRPRYNT
 1040 LDA#&12:STAprynt+&1
 1050 LDA#&5:STAprynt+&2
 1060 LDAtaget:JSRCONDI
 1070 LDAcondi:ASLA:ADCcondi:ASLA
 1080 TAX:LDY#&3
 1090 .alert
 1100 LDAcodes,X:STAprynt,Y
 1110 INX:INY:CPY#&9:BCCalert:TYA
 1120 LDX#prynt AND255:LDY#prynt DIV256
 1130 JSRPRYNT
 1140 LDA#&14:STAprynt+&1
 1150 LDA#&7:STAprynt+&2
 1160 LDAtaget:JSRTECHS:CLC
 1170 LDAtechs:ADC#ASC"A":STAprynt+&3
 1180 LDAtechs+&1:ADC#ASC"0":STAprynt+&4
 1190 LDX#prynt AND255:LDY#prynt DIV256
 1200 LDA#&5:JSRPRYNT
 1210 LDAtaget:JSRCONDI
 1220 LDAberth:STAtempa
 1230 STZtempx:STZindir
 1240 LDA#&C0:STAindir+&1
 1250 LDX#&14:LDY#&9:JSRDECIM
 1260 LDAtaget:JSRCONDI
 1270 LDAdepth:STAtempa
 1280 STZtempx:STZindir
 1290 LDA#&C0:STAindir+&1
 1300 LDX#&14:LDY#&B:JSRDECIM
 1310 LDAtaget:AND#&F
 1320 CLC:ADC#96:JSRWORDS
 1330 LDA#&12:STAprynt+&1
 1340 LDA#&D:STAprynt+&2
 1350 LDY#&3
 1360 .trans
 1370 LDAsixtn-&3,Y:STAprynt,Y
 1380 BEQgotit:INY:BNEtrans
 1390 .gotit
 1400 TYA
 1410 LDX#prynt AND255:LDY#prynt DIV256
 1420 JSRPRYNT
 1430 LDAtaget:JSRCONDI
 1440 LDAvolum:STAtempa:STZtempx
 1450 LDAvolum+&1:STAindir
 1460 LDA#&C0:STAindir+&1
 1470 LDX#&12:LDY#&F:JMPDECIM
 1480  
 1490 .codes
 1500 EQUS"Red   "
 1510 EQUS"Amber "
 1520 EQUS"Yellow"
 1530 EQUS"Green "
 1540 .prynt
 1550 EQUW&1F:EQUB0
 1560 .block
 1570 EQUD0:EQUD0
 1580 EQUD0:EQUD0
 1590  
 1600 .B
 1610 ASLA:ROLtwice+&1:ASLA:ROLtwice+&1
 1620 STAtwice:LDAtwice+&1:AND#&3
 1630 CLC:ADC#CHART DIV256:STAtwice+&1
 1640 LDY#&3:LDA(twice),Y:EOR#&FF
 1650 LDAtwice:LSRA
 1660 AND#&3:STAcondi:BEQzeros
 1670 BITgalax:BPLzeros:DECcondi
 1680 .zeros
 1690 LDA(twice),Y:LSRA:LSRA
 1700 AND#&7:ORA#&1:STAberth
 1710 DEY:LDA(twice),Y:EOR#&7
 1720 AND#&7:ORA#&1:STAdepth
 1730 DEY:LDA(twice),Y:EOR#&7
 1740 AND#&7:ORA#&1:STAvolum
 1750 LDA(twice):STAvolum+&1
 1760 RTS
 1770  
 1780 .C
 1790 LDX#&7:STXtempx
 1800 .wares
 1810 LDYprogs,X:LDAmoney,Y
 1820 CMPalred,X:BEQinvis:STAalred,X
 1830 LDA#&0:LDYalred,X
 1840 BNEgones:LDAshape,X
 1850 .gones
 1860 PHA:LDYypost,X:LDAxpost,X
 1870 TAX:PLA:JSRMDULE
 1880 .invis
 1890 DECtempx:LDXtempx:BPLwares
 1900 LDA#&2:STAtempa
 1910 .trice
 1920 LDXtempa:LDAlists,X:TAY
 1930 LDAmoney,Y:CMPholds,X:BEQnexts
 1940 STAholds,X:STAindir
 1950 LDX#&4:STXtempx
 1960 .torps
 1970 LDYtempa:LDAhuess,Y
 1980 CPXindir
 1990 BCCfears:BEQfears:LDA#&0
 2000 .fears
 2010 PHA:TXA:CLC:ADC#25:TAX
 2020 LDA#&6:SEC:SBCtempa:TAY
 2030 PLA:JSRMDULE
 2040 DECtempx:LDXtempx:BNEtorps
 2050 .nexts
 2060 DECtempa:BPLtrice
 2070 .vital
 2080 LDAchaff:CMPholds+&3
 2090 BEQdeprt:STAholds+&3
 2100 LDX#&3:STXtempx
 2110 LDA#&0:BITholds+&3
 2120 BMInegat:LDA#&70
 2130 .negat
 2140 STAtempa
 2150 .alums
 2160 LDAtempx:CLC:ADC#26:TAX
 2170 LDAtempa:LDY#&3:JSRMDULE
 2180 DECtempx:BPLalums
 2190 .deprt RTS
 2200  
 2210 .progs
 2220 EQUBeurek:EQUBphexa
 2230 EQUBheadu:EQUBnavig
 2240 EQUBrentr:EQUBmissn
 2250 EQUBfinan:EQUBmmodl
 2260 .xpost
 2270 EQUB10:EQUB11:EQUB12:EQUB13
 2280 EQUB10:EQUB11:EQUB12:EQUB13
 2290 .ypost
 2300 EQUB5:EQUB5:EQUB5:EQUB5
 2310 EQUB6:EQUB6:EQUB6:EQUB6
 2320 .shape
 2330 EQUD&AEAEE00E
 2340 EQUD&AEAEAEAE
 2350 .lists
 2360 EQUBminut:EQUBsmine
 2370 EQUBmagns
 2380 .huess
 2390 EQUB&77:EQUB&25
 2400 EQUB&72
 2410  
 2420 .D
 2430 LDAbuffr+&1F:BEQdeprt
 2440 LDA#&0:JSRNOISE
 2450 .reles
 2460 JSRTESTS:BNEreles
 2470 .pause
 2480 JSRTESTS:BEQpause
 2490 .final
 2500 JSRTESTS:BNEfinal
 2510 JMPNOISE
 2520 .TESTS
 2530 JSRBUFFR:LDAbuffr+&1F:RTS
 2540  
 2550 ]:NEXT

