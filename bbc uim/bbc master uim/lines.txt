   10 REM >LINES
   20 *K.0MO.7|MVDU14|ML.3760,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~O%,~P%|M
   60 *K.4*SAVE :1.C.CODES 5800 +0400 1800 1800|M
   70 *K.10O.|M*FX12,2|M
   80 *TV255,1
   90 speed=&4
  100  
  110 steal=&0
  120 letgo=&1
  130 grabs=&2
  140 image=&E
  150 muscl=&F
  160 split=&15
  170 ident=&17
  180 distn=&18
  190 face1=&19
  200 face2=&1A
  210 face3=&1B
  220 storx=&1C
  230 story=&1D
  240 rolla=&1E
  250 rollb=&1F
  260 xsrot=&20
  270 ysrot=&21
  280 zsrot=&22
  290 terps=&2A
  300 death=&2D
  310 vapor=&2E
  320 flite=&33
  330 dials=&40
  340 partr=&50
  350 remms=&52
  360 rnums=&54
  370 corns=&58
  380 curve=&5C
  390 taget=&5D
  400 tcols=&64
  410 tempa=&70
  420 tempx=&71
  430 tempy=&72
  440 tempz=&73
  450 indir=&74
  460 count=&76
  470 works=&77
  480 opton=&82
  490 prodt=&85
  500 sitop=&87
  510 sibot=&88
  520 mcand=&89
  530 mplir=&8B
  540 strax=&90
  550 stray=&92
  560 endex=&94
  570 endey=&96
  580 archi=&A0
  590 STACK=&100
  600 plant=&A07
  610 fuels=&A09
  620 presy=&A0C
  630 ships=&A0D
  640 headu=&A3F
  650 eurek=&A44
  660 drive=&A4B
  670 buffr=&AD0
  680 PDATA=&B06
  690 ODATA=&B09
  700 ARMOV=&400
  710 AROFF=&403
  720 ENTRY=&409
  730 WORLD=&40C
  740 ENERG=&40F
  750 START=&1200
  760 WIPES=&1209
  770 SPASH=&120F
  780 SAILS=&1212
  790 PLACE=&121B
  800 FERMO=&1C0C
  810 BASES=&2280
  820 DEATH=&2E00
  830 MANIP=&2E06
  840 CTRLS=&2E09
  850 BOXIT=&2E21
  860 MCODE=&3A8F
  870 RANDY=&3A92
  880 ATUDE=&3A95
  890 FEBAK=&4203
  900 DOLAR=&4209
  910 RMSAV=&4D92
  920 INFRM=&5778
  930 PENCL=&9803
  940 NOISE=&9806
  950 GRATE=&9C03
  960 VERTZ=&9C06
  970 HYPEL=&9C0C
  980 INDEX=&9C0F
  990 SHOWS=&AF03
 1000 CLEAN=&AF06
 1010 HAIRS=&AF15
 1020 BARRS=&AF27
 1030 BUFFR=&B583
 1040 RELAT=&B780
 1050 CALCD=&B783
 1060 DRAIN=&B792
 1070 FORA%=4TO6STEP2
 1080 O%=&5800
 1090 P%=&1800
 1100 [OPTA%
 1110 .POLYS JMPA
 1120 .FACES JMPB
 1130 .VIEWS JMPC
 1140 .FLITE JMPD
 1150 .LANCH JMPE
 1160  
 1170 .A
 1180 LDAident:TAY:ASLA:TAX
 1190 LDABASES+&40,X:STAtempa
 1200 LDABASES+&41,X:STAtempx
 1210 LDABASES+&A0,Y:STAtempy
 1220 STZtempz:STZarchi
 1230 LDA#&7F:STAdraws+&4
 1240 .draws
 1250 INCdraws+&4:BITSTACK+&80
 1260 BMIbipas
 1270 JSRSURFS
 1280 DECtempy:BNEdraws:RTS
 1290 .bipas
 1300 LDYtempz:INY:INY:INY:STYtempz
 1310 DECtempy:BNEdraws:RTS
 1320  
 1330 .SURFS
 1340 LDYtempz
 1350 LDA(tempa),Y:STAface1:INY:TAX
 1360 LDA(tempa),Y:STAface2:INY
 1370 LDA(tempa),Y:STAface3:INY
 1380 STYtempz:LDYface2
 1390 STXindir:STYindir+&1
 1400 LDASTACK,X:STAstrax
 1410 STArnums
 1420 LDASTACK+&40,X:STAstrax+&1
 1430 STArnums+&1
 1440 LDASTACK+&20,X:STAstray
 1450 STArnums+&2
 1460 LDASTACK+&60,X:STAstray+&1
 1470 STArnums+&3
 1480 LDASTACK,Y:STAendex
 1490 STAcorns
 1500 LDASTACK+&40,Y:STAendex+&1
 1510 STAcorns+&1
 1520 LDASTACK+&20,Y:STAendey
 1530 STAcorns+&2
 1540 LDASTACK+&60,Y:STAendey+&1
 1550 STAcorns+&3
 1560 JSRSERCH:BCSsames
 1570 JSRPENCL
 1580 .sames
 1590 ]PROCswap(corns,strax):[OPTA%
 1600 LDAface3:AND#&1F:TAX:STXindir
 1610 LDASTACK,X:STAendex
 1620 STAcorns
 1630 LDASTACK+&40,X:STAendex+&1
 1640 STAcorns+&1
 1650 LDASTACK+&20,X:STAendey
 1660 STAcorns+&2
 1670 LDASTACK+&60,X:STAendey+&1
 1680 STAcorns+&3
 1690 JSRSERCH:BCSinvis
 1700 JSRPENCL
 1710 .invis
 1720 BITface3:BMIretun
 1730 LDAface1:STAindir+&1
 1740 JSRSERCH:BCSretun
 1750 ]PROCswap(corns,strax):[OPTA%
 1760 ]PROCswap(rnums,endex):[OPTA%
 1770 JMPPENCL
 1780  
 1790 .SERCH
 1800 LDXarchi
 1810 LDAindir:STAarchi+&1,X
 1820 LDAindir+&1:STAarchi+&2,X
 1830 TXA:BEQfirst
 1840 .alist
 1850 LDAarchi,X:LDYarchi-&1,X
 1860 CMPindir+&1:BNEpexts
 1870 CPYindir:BEQretun
 1880 .pexts
 1890 CMPindir:BNEalong
 1900 CPYindir+&1:BEQretun
 1910 .along
 1920 DEX:DEX:BNEalist
 1930 .first
 1940 INCarchi:INCarchi:CLC
 1950 .retun RTS
 1960  
 1970 .B
 1980 LDAident:TAY:ASLA:TAX
 1990 LDABASES+&40,X:STAtempa
 2000 LDABASES+&41,X:STAtempx
 2010 LDABASES+&A0,Y:STAtempy
 2020 STZtempz:STZworks
 2030 .check
 2040 LDYworks
 2050 LDA(tempa),Y:STAindir:TAX:INY
 2060 LDA(tempa),Y:STAindir+&1:INY
 2070 LDA(tempa),Y:AND#&1F:STAcount
 2080 INY:STYworks:LDYindir+&1
 2090 LDASTACK,X:SEC
 2100 SBCSTACK,Y:STAmcand
 2110 LDASTACK+&40,X:SBCSTACK+&40,Y
 2120 STAmcand+&1
 2130 LDXcount
 2140 LDASTACK+&20,X:SEC
 2150 SBCSTACK+&20,Y:STAmplir
 2160 LDASTACK+&60,X:SBCSTACK+&60,Y
 2170 JSRMULTY
 2180 LDAprodt:STAcorns
 2190 LDAprodt+&1:STAcorns+&1
 2200 LDXindir:LDYindir+&1
 2210 LDASTACK+&20,X:SEC
 2220 SBCSTACK+&20,Y:STAmcand
 2230 LDASTACK+&60,X:SBCSTACK+&60,Y
 2240 STAmcand+&1
 2250 LDXcount
 2260 LDASTACK,X:SEC
 2270 SBCSTACK,Y:STAmplir
 2280 LDASTACK+&40,X:SBCSTACK+&40,Y
 2290 JSRMULTY
 2300 LDAcorns:CMPprodt
 2310 LDAcorns+&1:SBCprodt+&1
 2320 INCtempz:LDXtempz
 2330 STASTACK+&7F,X:CPXtempy
 2340 BEQdummy:JMPcheck
 2350  
 2360 .MULTY
 2370 STAmplir+&1:LDX#&0
 2380 STAsibot:TAY:BPLsodit
 2390 TXA:SEC:SBCmplir:STAmplir
 2400 TXA:SBCmplir+&1:STAmplir+&1
 2410 .sodit
 2420 LDAmcand+&1:STAsitop:BPLleave
 2430 TXA:SEC:SBCmcand:STAmcand
 2440 TXA:SBCmcand+&1:STAmcand+&1
 2450 .leave
 2460 LDAmplir
 2470 ASLA:ROLmplir+&1
 2480 ASLA:ROLmplir+&1
 2490 ASLA:ROLmplir+&1
 2500 ASLA:ROLmplir+&1
 2510 ASLA:ROLmplir+&1
 2520 ASLA:ROLmplir+&1
 2530 STAmplir
 2540 TXA:LDX#&A
 2550 .nexts
 2560 ASLA:ROLprodt+&1
 2570 ASLmplir:ROLmplir+&1:BCCendof
 2580 CLC:ADCmcand:TAY
 2590 LDAprodt+&1:ADCmcand+&1
 2600 STAprodt+&1:TYA
 2610 .endof
 2620 DEX:BNEnexts:STAprodt
 2630 LDAsitop:EORsibot:BPLdummy
 2640 TXA:SEC:SBCprodt:STAprodt
 2650 TXA:SBCprodt+&1:STAprodt+&1
 2660 .dummy RTS
 2670  
 2680 .C
 2690 JSRCLEAN
 2700 JSRFEBAK
 2710 JSRDOLAR
 2720 JSRATUDE
 2730 JSRRETUD
 2740 JSRDRAIN
 2750 JSRMANIP
 2760 JSRINFRM
 2770 JMPCTRLS
 2780  
 2790 .RETUD
 2800 LDAeurek:ORAheadu:BMIcross
 2810 JSRBOXIT
 2820 LDX#&0:JSRCHANG
 2830 LDX#&1:JSRCHANG
 2840 LDAstorx:JSRGRATE
 2850 LDAstory:JMPVERTZ
 2860 .cross
 2870 JMPHAIRS
 2880  
 2890 .CHANG
 2900 LDArolla,X:TAY:AND#&7:BEQresum
 2910 ASLA:ASLA
 2920 CPY#&8:BCCfines:STAtempa
 2930 LDA#&0:SEC:SBCtempa
 2940 .fines
 2950 ADCstorx,X:CMP#&20
 2960 BCSupper:LDA#&20
 2970 .upper
 2980 CMPlimiy,X:BCCokays:LDAlimiy,X
 2990 .okays
 3000 STAstorx,X:RTS
 3010 .resum
 3020 LDAstorx,X:CMP#&80:BEQmoves
 3030 ASLA:LDA#&0:ROLA:TAY
 3040 LDAstorx,X:ADCtable,Y:STAstorx,X
 3050 .moves RTS
 3060  
 3070 .table EQUW&FC04
 3080 .limiy EQUW&E0D0
 3090  
 3100 .D
 3110 LDAbuffr+&1C:BEQcheer
 3120 LDAbuffr+&2D:BNEocean
 3130 JSRRELAT
 3140 LDA#&A:LDXtaget
 3150 CPXplant:BEQtofar
 3160 JSRCALCD
 3170 LDAfuels+&1:SEC:SBCremms:TAX
 3180 LDAfuels:SBCpartr:TAY:LDA#&0
 3190 BCCtofar:STYfuels:STXfuels+&1
 3200 LDA#&D:BCSSEQUN
 3210 .cheer RTS
 3220 .tofar
 3230 LDXvapor:BPLcheer:STAvapor
 3240 LDA#&1:JMPNOISE
 3250 .ocean
 3260 BITdrive:BMIcheer:DECdrive
 3270 JSRRELAT
 3280 LDA#&A:LDXtaget
 3290 CPXplant:BEQtofar
 3300 LDA#&E
 3310  
 3320 .SEQUN
 3330 STAvapor
 3340 JSRWIPES
 3350 JSRCLEAN
 3360 JSRSHOWS
 3370 STZtcols
 3380 LDA#ASC"A":JSRMCODE
 3390 LDX#&0:STXpresy
 3400 DEX:STXdials+&4
 3410 LDA#&E0:STAflite
 3420 JSRINDEX
 3430 .travl
 3440 JSRCLEAN
 3450 JSRRANDY
 3460 JSRBUFFR
 3470 JSRARMOV
 3480 JSRENTRY
 3490 JSRFERMO
 3500 JSRENERG
 3510 JSRFEBAK
 3520 LDAcurve:BEQnmore
 3530 LDA#&10:STAvapor
 3540 .nmore
 3550 LDAflite:ASLA:ROLA:ROLA:ROLA
 3560 AND#&7:TAX:LDAmulty,X
 3570 JSRWORLD
 3580 JSRBARRS
 3590 JSRDEATH
 3600 JSRSHOWS
 3610 DECflite:BNEtravl
 3620 LDAtaget:STAplant
 3650 LDA#&20:STAdials+&4
 3660 LDA#&6:STAopton+&1
 3670 JSRCLEAN
 3680 JSRSHOWS
 3690 JSRODATA
 3691 LDA#&9:JSRNOISE
 3692 LDA#&A:JSRNOISE
 3700 JSRSTART
 3710 JSRSPASH
 3720 JMPPLACE
 3730 .dpart RTS
 3740  
 3750 .multy
 3760 EQUD&2030405:EQUD&5040302
 3770  
 3780 .E
 3790 LDX#&7
 3800 .clean
 3810 STZdials+&8,X
 3820 STZxsrot,X
 3830 DEX:BPLclean
 3840 STZterps:STZrolla:STZrollb
 3850 STZflite:STZsplit:STZdeath
 3860 STZletgo
 3870 LDA#&80:STAships
 3880 STAstorx:STAstory
 3890 LDA#&20:STAdials+&4
 3900 LDA#&FF
 3910 STAdials:STAdials+&1
 3920 STAdials+&2:STAdials+&3
 3930 STAsteal:STAgrabs
 3940 STAvapor:STAimage
 3950 STAmuscl
 3960 JSRCLEAN
 3970 JSRSHOWS
 3980 LDA#&5:STAtcols
 3990 JSRODATA
 4000 JSRHYPEL
 4010 LDX#&6:STXopton+&1
 4020 DEX:STXtcols
 4030 JSRPLACE
 4040 JSRSTART
 4050 JMPSAILS
 4060  
 4070 ]:NEXT:END
 4080  
 4090 DEFPROCswap(sorc%,dest%)
 4100 [OPTA%
 4110 LDAsorc%:STAdest%
 4120 LDAsorc%+&1:STAdest%+&1
 4130 LDAsorc%+&2:STAdest%+&2
 4140 LDAsorc%+&3:STAdest%+&3
 4150 ]
 4160 ENDPROC
 4170  

